pragma ComponentBehavior: Bound
import qs
import qs.modules.common
import qs.modules.common.widgets
import qs.services
import QtQuick
import Quickshell
import Quickshell.Io
import Quickshell.Wayland
import Quickshell.Hyprland

Scope {
    id: root

    function toggle() {
        const name = Hyprland.focusedMonitor?.name;
        if (!name) return;
        const monitors = GlobalStates.oledSaverMonitors;
        GlobalStates.oledSaverMonitors = monitors.includes(name) ? monitors.filter(n => n !== name) : [...monitors, name];
    }

    function close(name) {
        GlobalStates.oledSaverMonitors = GlobalStates.oledSaverMonitors.filter(n => n !== name);
    }

    // Inhibits sleep/lock/DPMS while at least one monitor is blacked out.
    // Kept independent from the shared Idle.inhibit toggle so this feature
    // never clobbers the user's own manual idle-inhibit preference.
    IdleInhibitor {
        enabled: GlobalStates.oledSaverMonitors.length > 0
        window: PanelWindow {
            implicitWidth: 0
            implicitHeight: 0
            color: "transparent"
            anchors {
                right: true
                bottom: true
            }
            mask: Region {
                item: null
            }
        }
    }

    IpcHandler {
        target: "oledSaver"

        function toggle() {
            root.toggle();
        }
    }

    GlobalShortcut {
        name: "oledSaverToggle"
        description: "Toggles the OLED saver (blackout) on the focused monitor"
        onPressed: root.toggle()
    }

    component OledSaverWindow: PanelWindow {
        id: window
        signal dismiss

        color: "black"
        WlrLayershell.namespace: "quickshell:oledSaver"
        // Top (not Overlay) so OSD, notifications, polkit prompts, etc. still
        // render above the blackout instead of being hidden by it.
        WlrLayershell.layer: WlrLayer.Top
        // OnDemand (not Exclusive) so only this monitor's input is captured;
        // Exclusive grabs all input globally, blocking mouse on other monitors.
        WlrLayershell.keyboardFocus: WlrKeyboardFocus.OnDemand
        exclusionMode: ExclusionMode.Ignore
        anchors {
            top: true
            bottom: true
            left: true
            right: true
        }

        // Explicit mask constrains input to this window's bounds on this output only.
        mask: Region {
            item: windowMaskItem
        }

        // Start the same way a mouse move leaves things: cursor and hint shown,
        // both hide timers already counting down.
        property bool cursorVisible: true
        property bool hintVisible: true

        // A focus grab held while this window's surface is still mapping keeps
        // the pointer focused on whatever is underneath, so Qt never learns the
        // cursor is inside and can never swap it for the blank one - the old
        // cursor image would sit on the blackout until the user moved the mouse.
        // Arming the grab a moment after the surface is up lets the pointer
        // enter land first, then still routes Esc here without a click.
        HyprlandFocusGrab {
            id: oledGrab
            windows: [window]
            active: false
        }

        Timer {
            running: true
            interval: 100
            onTriggered: oledGrab.active = true
        }

        Item {
            id: windowMaskItem
            anchors.fill: parent
            focus: true

            Keys.onPressed: event => {
                if (event.key === Qt.Key_Escape)
                    window.dismiss();
            }

            MouseArea {
                anchors.fill: parent
                hoverEnabled: true
                cursorShape: window.cursorVisible ? Qt.ArrowCursor : Qt.BlankCursor

                onPositionChanged: {
                    window.cursorVisible = true;
                    window.hintVisible = true;
                    cursorHideTimer.restart();
                    hintHideTimer.restart();
                }
                onClicked: window.dismiss()
            }

            StyledText {
                anchors.centerIn: parent
                text: Translation.tr("Press Esc or click to exit")
                color: "white"
                font.pixelSize: Appearance.font.pixelSize.large
                opacity: window.hintVisible ? 1 : 0
                visible: opacity > 0

                Behavior on opacity {
                    NumberAnimation {
                        duration: Appearance.animation.elementMoveFast.duration
                        easing.type: Appearance.animation.elementMoveFast.type
                        easing.bezierCurve: Appearance.animation.elementMoveFast.bezierCurve
                    }
                }
            }

            Timer {
                id: cursorHideTimer
                running: true
                interval: Config.options.oledSaver.cursorHideDelay * 1000
                onTriggered: window.cursorVisible = false
            }

            Timer {
                id: hintHideTimer
                running: true
                interval: (Config.options.oledSaver.cursorHideDelay + Config.options.oledSaver.hintExtraDelay) * 1000
                onTriggered: window.hintVisible = false
            }
        }
    }

    Variants {
        model: Quickshell.screens

        delegate: Loader {
            id: oledSaverLoader
            required property var modelData
            active: GlobalStates.oledSaverMonitors.includes(modelData.name)

            sourceComponent: OledSaverWindow {
                screen: oledSaverLoader.modelData
                onDismiss: root.close(oledSaverLoader.modelData.name)
            }
        }
    }
}
