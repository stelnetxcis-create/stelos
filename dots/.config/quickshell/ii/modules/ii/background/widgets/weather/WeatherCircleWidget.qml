import QtQuick
import QtQuick.Layouts
import Qt5Compat.GraphicalEffects
import qs
import qs.services
import qs.modules.common
import qs.modules.common.functions
import qs.modules.common.widgets
import qs.modules.ii.background.widgets

AbstractBackgroundWidget {
    id: root

    configEntryName: "weather_circle"

    implicitWidth: 240
    implicitHeight: 240

    readonly property color outerCircleColor: WidgetColorScheme.pillBgColor
    readonly property color cookieBgColor: WidgetColorScheme.cardBgColor
    readonly property color textColorOnCookie: WidgetColorScheme.textColorOnBg

    StyledDropShadow {
        id: shadowEffect
        target: outerCircle
        visible: Config.options.background.widgets.enableShadows ?? true
    }

    Rectangle {
        id: outerCircle
        anchors.centerIn: parent
        width: Math.min(root.width, root.height)
        height: width
        radius: width / 2
        color: root.outerCircleColor

        MaterialShape {
            id: innerCookie
            anchors.centerIn: parent
            implicitSize: outerCircle.width * 0.90
            shapeString: "Cookie12Sided"
            color: "transparent"

            MaterialShape {
                id: cookieBg
                anchors.fill: parent
                shapeString: parent.shapeString
                color: root.cookieBgColor
                visible: !(Config.options.background.widgets.enableInnerShadow ?? true)
            }

            InnerShadow {
                id: innerShadow
                anchors.fill: parent
                radius: 20
                samples: 41
                color: Qt.rgba(0, 0, 0, 0.40)
                source: cookieBg
                visible: Config.options.background.widgets.enableInnerShadow ?? true
            }

            ColumnLayout {
                anchors.centerIn: parent
                spacing: -6

                Image {
                    Layout.alignment: Qt.AlignHCenter
                    source: WeatherIcons.getWeatherIcon(Weather.data?.wCode ?? 113, false)
                    sourceSize: Qt.size(96, 96)
                }

                RowLayout {
                    Layout.alignment: Qt.AlignHCenter
                    spacing: 4

                    StyledText {
                        text: Weather.data?.temp ? Weather.data.temp.replace("°C", "°").replace("°F", "°") : ""
                        color: root.textColorOnCookie
                        font.pixelSize: Appearance.font.pixelSize.large
                        font.weight: Font.DemiBold
                    }

                    StyledText {
                        text: Weather.data?.city || ""
                        color: root.textColorOnCookie
                        font.pixelSize: Appearance.font.pixelSize.large
                        font.weight: Font.DemiBold
                        elide: Text.ElideRight
                        Layout.maximumWidth: 100
                    }
                }
            }
        }
    }
}
