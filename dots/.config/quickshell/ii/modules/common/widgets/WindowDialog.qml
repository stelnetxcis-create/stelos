import QtQuick
import QtQuick.Layouts
import QtQuick.Effects
import Quickshell
import qs.modules.common
import qs.modules.common.functions
import qs.modules.common.widgets

Rectangle {
    id: root

    property bool show: false
    default property alias contentData: contentColumn.data
    readonly property real contentHeight: contentColumn.implicitHeight + dialogBackground.radius * 2
    property real backgroundHeight: contentHeight
    // An owner can opt into a wider dialog without changing existing
    // backgroundWidth overrides used by other hosts.
    property real preferredDialogWidth: 0
    property real backgroundWidth: 350
    property real backgroundAnimationMovementDistance: 60
    
    signal dismiss()
    Keys.onPressed: (event) => {
        if (event.key === Qt.Key_Escape) {
            root.dismiss();
            event.accepted = true;
        }
    }

    color: root.show ? Appearance.colors.colScrim : ColorUtils.transparentize(Appearance.colors.colScrim)
    Behavior on color {
        animation: Appearance.animation.elementMoveFast.colorAnimation.createObject(this)
    }
    visible: dialogBackground.opacity > 0.01

    radius: Appearance.rounding.screenRounding - Appearance.sizes.hyprlandGapsOut + 1

    MouseArea { // Clicking outside the dialog should dismiss
        anchors.fill: parent
        acceptedButtons: Qt.AllButtons
        hoverEnabled: true
        onPressed: root.dismiss()
        onWheel: (wheel) => wheel.accepted = true
    }

    Rectangle {
        id: dialogBackground
        anchors.horizontalCenter: parent.horizontalCenter
        radius: Appearance.rounding.large
        color: Appearance.m3colors.m3surfaceContainerHigh // Use opaque version of layer3
        
        property real targetY: root.height / 2 - root.backgroundHeight / 2
        y: root.show ? targetY : (targetY + 40)
        scale: root.show ? 1.0 : 0.88
        opacity: root.show ? 1.0 : 0.0

        implicitWidth: root.preferredDialogWidth > 0 ? root.preferredDialogWidth : root.backgroundWidth
        implicitHeight: root.backgroundHeight

        Behavior on y {
            NumberAnimation {
                duration: 350
                easing.type: root.show ? Easing.OutBack : Easing.InCubic
                easing.overshoot: 1.2
            }
        }

        Behavior on scale {
            NumberAnimation {
                duration: 350
                easing.type: root.show ? Easing.OutBack : Easing.InCubic
                easing.overshoot: 1.2
            }
        }

        Behavior on opacity {
            NumberAnimation {
                duration: root.show ? 280 : 200
                easing.type: Easing.OutCubic
            }
        }

        MouseArea { // So clicking inside the dialog won't dismiss
            anchors.fill: parent
            acceptedButtons: Qt.AllButtons
            hoverEnabled: true
            onWheel: (wheel) => wheel.accepted = true
        }

        ColumnLayout {
            id: contentColumn
            anchors {
                fill: parent
                margins: dialogBackground.radius
            }
            spacing: 16
            opacity: root.show ? 1 : 0
            Behavior on opacity {
                animation: Appearance.animation.elementMoveFast.numberAnimation.createObject(this)
            }

        }
    }
}
